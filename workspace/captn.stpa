Losses
L1 "Loss of life or serious injury to people"
L2 "Damage to the ferry or objects outside the ferry"

Hazards
H1 "Loss of vessel control" [L1, L2]
H2 "Vessel's exposure to major damage or breakdown" [L1, L2] {
    H2.1 "Vessel enters a No Go Area"
    H2.2 "Vessel violates minimum distance to another ship"
    H2.3 "Vessel's power supply is not provided or insufficient"
}

SystemConstraints
SC2 "Vessel must not be exposed to major damage or breakdown" [H2] {
    SC2.1 "Vessel must not enter a No Go Area" [H2.1]
    SC2.2 "Vessel must keep safe distance to other ships" [H2.2]
}

ControlStructure
Ferry {
    ControlCentre {
        hierarchyLevel 0
        controlActions {
            [controls "Other controls" ]-> OtherSubsystems
            [navi "Route navigation", data "Weather data"] -> VirtualCaptain 
            [manual "Manual setting"] -> Engine
        }
    }
    OtherSubsystems {
        hierarchyLevel 1
        feedback {
            [modes "Other system feedback"] -> ControlCentre
        }
    }
    VirtualCaptain {
        hierarchyLevel 1
        controlActions {
            [pars "Set parameters"] -> Engine
        }
        feedback {
            [status "System status", faults "Faults"] -> ControlCentre 
        }
    }
    Engine {
        hierarchyLevel 2
        feedback {
            [motion "Motion"] -> VirtualCaptain
        }
    }
}

Responsibilities
VirtualCaptain {
    R1 "Actuate reverse gear when too close to a No Go Area" [SC2.1]
    R2 "Initiate evasive action when too close to another ship" [SC2.2]
}
ControlCentre {
    R3 "Manually set parameters of engine in case of a malfunction" [SC2.1, SC2.2]
}

UCAs
ControlCentre.manual {
    notProviding {
        UCA1 "ControlCentre does not provide Manual setting when VC is malfunctioning and vessel too close to a No Go Area" [H2.1]
    }
    providing {
    }
    tooEarly/Late {
    }
    stoppedTooSoon {
    }
}
VirtualCaptain.pars {
    notProviding {
    }
    providing {
    }
    tooEarly/Late {
        UCA2 "VirtualCaptain provides set parameters too late (>TBD seconds) when vessel comes too close to a No Go Area" [H2.1]
    }
    stoppedTooSoon {
    }
}

ControllerConstraints
C1 "ControlCentre must provide the Manual setting control action during VC malfunctioning and vessel too close to No Go Area" [UCA1]
C3 "Virtual Captain must provide the Set parameters control action within TBD seconds after coming too close to a No Go Area" [UCA2]

LossScenarios
Scenario1 for UCA1 "Abnormal vessel behavior occurs. Vessel comes too close to a No Go Area and ControlCentre does not manual set the parameters of the engine, causing the entering of a No Go Area." [H2.1]
Scenario2 "Virtual Captain sends the Set parameters command upon coming too close to a No Go Area, but decceleration is not applied due to actuator failure." [H2.1]
